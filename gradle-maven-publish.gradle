/*
 * Copyright 2013 Square, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'signing'
apply plugin: 'maven-publish'

def isReleaseBuild() {
  return false
//  return !VERSION_NAME.contains("SNAPSHOT")
}

afterEvaluate {
  tasks.register("androidSourcesJar", Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.sourceFiles
  }

  tasks.register("androidJavadocsJar", Jar) {
    archiveClassifier.set('javadoc')
    from androidJavadocs.destinationDir
  }
  tasks.named("androidJavadocsJar").configure {
    dependsOn tasks.named("androidJavadocs")
  }

  tasks.register("androidJavadocs", Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += project.files(android.libraryVariants.collect { variant ->
      variant.javaCompileProvider.get().classpath.files
    })
  }

  publishing {
    repositories {
    }

    publications {
      release(MavenPublication) {
        from components.release
        artifact androidSourcesJar
        artifact androidJavadocsJar

        pom {
          name = POM_NAME
          description = POM_DESCRIPTION
          url = POM_URL
          packaging = POM_PACKAGING

          scm {
            url = POM_SCM_URL
            connection = POM_SCM_CONNECTION
            developerConnection = POM_SCM_DEV_CONNECTION
          }

          licenses {
            license {
              name = POM_LICENCE_NAME
              url = POM_LICENCE_URL
              distribution = POM_LICENCE_DIST
            }
          }

          developers {
            developer {
              organization = POM_ORGANIZATION_NAME
              organizationUrl = POM_ORGANIZATION_URL
            }
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications.release
    required { isReleaseBuild() }
  }
}
